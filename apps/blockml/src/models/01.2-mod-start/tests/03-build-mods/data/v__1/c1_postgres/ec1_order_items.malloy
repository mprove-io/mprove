import './parts/order_items.malloy';
import './parts/orders.malloy';
import './parts/users.malloy';
import './parts/user_order_facts.malloy';
import './parts/inventory_items.malloy';
import './parts/products.malloy';
import './parts/distribution_centers.malloy';

#(mprove) model access_roles=[sales, marketing]
source: ec1_order_items is order_items extend {
  join_one: orders on order_id = orders.order_id
  join_one: users on orders.user_id = users.user_id
  join_one: user_order_facts on users.user_id = user_order_facts.user_id
  join_one: inventory_items on inventory_item_id = inventory_items.inventory_item_id
  join_one: products on inventory_items.product_id = products.product_id
  join_one: distribution_centers on inventory_items.distribution_center_id = distribution_centers.distribution_center_id


  measure: total_profit is total_sale_price - inventory_items.total_actual_cost

  # percent
  measure: profit_margin is total_profit / total_sale_price

  -- # bar_chart
  view: view_orders_count_by_users_state is {
    top: 10
    group_by: users.state
    aggregate: orders.orders_count
  }

  view: view_min_first_order_created_ts_by_users_state is {
    top: 10
    group_by: users.state
    aggregate: min_first_order_created_at_ts is min(user_order_facts.first_order_created_at_ts)
  }
}
